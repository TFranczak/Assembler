//KC03I36A JOB ,'Tommy Franczak',MSGCLASS=H
//STEP1 EXEC PGM=ASSIST
//STEPLIB DD DSN=KC02293.ASSIST.LOADLIB,DISP=SHR
//SYSPRINT DD SYSOUT=*
//SYSIN DD *
********************************************
* Assignment 5 - Pair Programmed
* Names: Tommy Franczak, Matt Rycraft
*
* Function: Read in numbers from file and 
* build a table and then print out all 
* of the numbers and all the odd numbers
* 
* MAIN
* Register Usage
*
*  1     Parameter List
* 12     Base register
* 13     Main save
* 14     Return Address
********************************************
MAIN     CSECT
         STM   14,12,12(13)       Copy registers
         LR    12,15              Load 12 as base register
         USING MAIN,12            Use 12 as base register
         LA    14,MAINSAVE        Load MAINSAVE into R14
         ST    13,4(,14)          Link forward pointer
         ST    14,8(,13)          Link backward pointer
         LR    13,14              Load MAINSAVE
         SPACE
         LA    1,PLIST            Load PLIST into 1
         L     15,=V(BUILD)       Load virtual address of BUILD
         BALR  14,15              Do BUILD subroutine
         L     15,=V(PRINT)       Load virtual address of PRINT
         BALR  14,15              Do PRINT subroutine
         L     15,=V(SEARCH)      Load virtual address of SEARCH
         BALR  14,15              Do SEARCH subroutine
         SPACE
         L     13,4(0,13)         Load backward pointer
         LM    14,12,12(13)       Load registers back
         BR    14
         LTORG                    Literal origin
TABLE    DC    58F'-1'            Declare Table
EOT      DS    F                  Declare End of Table
TARGET   DS    F                  Number were searching for
LOC      DS    F                  Location of element in table
BUFFER   DS    80C                Declare Buffer
         DC    C'*'               End for XDECI
PLIST    DC    A(TABLE)           Parameter list
         DC    A(EOT)
         DC    A(BUFFER)
         DC    A(TARGET)
         DC    A(LOC)
MAINSAVE DS    18F
********************************************
* Build Subroutine
*
* Function: Reads in numbers from an 
* unknown file and builds a table of  
* 49 numbers
* 
* Register Usage
*
*  4     Table Pointer
*  5     EOT Pointer
*  6     Value in table
*  7     Division
*  8     Address of Outline variables
*  9     Address of buffer
* 12     Base register
* 13     Address of BUILDSV (save area)
* 14     Return Address
********************************************
BUILD    CSECT       
         STM   14,12,12(13)       Save registers
         LR    12,15              Load 12 as Base Register
         USING BUILD,12           Use 12 as Base Register 
         LA    14,BUILDSV         Load PRINTSV into 14
         ST    14,8(0,14)         Link backward pointer
         ST    13,4(0,14)         Link forward pointer
         LR    13,14              Load BUILDSV
         LM    4,5,0(1)           Unload parameters
         L     9,8(1)             Load address of buffer
BLOOP1   XREAD 0(0,9),80          Read line into buffer
         BC    B'0100',ENDBLP1    End of file?
         XDECI 6,0(0,9)           Read first number into R6
         B     DECIPHER           Branch into BLOOP2
BLOOP2   XDECI 6,0(0,1)           Read next number into R6
DECIPHER BC    B'0001',BLOOP1     At end of line?
         C     6,=F'-77777777'    At delimiter?
         BE    ENDBLP1            End loop
         ST    6,0(4)             Store number in table
         A     4,=F'4'            Increment table pointer
         B     BLOOP2             Back to BLOOP2
ENDBLP1  ST    4,0(5)             Store end of table
         L     13,4(0,13)         Load register list
         LM    14,12,12(13)        Reload registers
         BR    14                 Branch back to MAIN
         LTORG
BUILDSV  DS    18F                Subroutine save
         SPACE
         SPACE
         SPACE
********************************************
* Print Subroutine
*
* Function: Prints 6 numbers to a line from 
* the table until the EOT is found
* 
* Register Usage
*
*  4     Table Pointer
*  5     EOT Pointer
*  6     Value in table
*  7     Division
*  8     Address of Outline variables
*  9     Counter variable
* 12     Base register
* 13     Address of PRINTSV (save area)  
* 14     Return Address
********************************************
PRINT    CSECT 
         STM   14,12,12(13)       Save registers
         LR    12,15              Load 12 as Base Register
         USING PRINT,12           Use 12 as Base Register 
         LA    14,PRINTSV         Load PRINTSV into 14
         ST    14,8(0,14)         Link backward pointer
         ST    13,4(0,14)         Link forward pointer
         LR    13,14              Load SEARCHSV
         LM    4,5,0(1)           Unload parameters
         XPRNT HEADER,37          Print header line
         SR    9,9                Zero loop counter
PLOOP1   LA    8,NUMS             Load address of NUMS
         MVI   LINE,C' '          Clear LINE
         MVC   LINE+1(84),LINE    Clear LINE
PLOOP2   C     4,0(0,5)           EOT = Table Ptr?
         BE    ENDPLP             Go to ENDPLOOP
         L     6,0(0,4)           Load Table Value into R6
         XDECO 6,0(0,8)           Send num to NUMS
         A     4,=F'4'            Increment table pointer
         A     8,=F'12'           Increment NUMS pointer
         A     9,=F'1'            Increment Loop counter
         C     9,=F'7'            7 on the line?
         BE    PRNTLN             Go to print line
         B     PLOOP2             Again
PRNTLN   XPRNT LINE,85            Print line
         SR    9,9                Zero loop counter
         B     PLOOP1             Again
ENDPLP   C     9,=F'0'            Is counter 0?
         BE    SKIPLAST           Skip last print
         XPRNT LINE,85            Print last line from table
SKIPLAST L     13,4(0,13)         Load register list
         LM    14,12,12(13)       Reload registers
         BR    14                 Branch back to MAIN
         LTORG
HEADER   DC    C'1'               Format for header line - 37
         DC    20C' '
         DC    CL16'List of numbers:'
LINE     DC    C'0'               Declare print line - 85
NUMS     DS    7CL12              Declare array of nums
PRINTSV  DS    18F                Create register save area
         SPACE
         SPACE
         SPACE
********************************************
* Search Subroutine
*
* Function: Searches the table for an integer 
* 
* Register Usage
*
*  2     Table address
*  3     EoT Pointer
*  4     Buffer Pointer
*  5     Target Pointer
*  6     Location Pointer
*  7     Number Read
*  8     Table pointer (incrementing)
* 12     Base register
* 13     Address of SEARCHSV (save area)
* 14     Return Address
******************************************** 
SEARCH   CSECT
         STM   14,12,12(13)       Save registers
         LR    12,15              Load 12 as Base Register
         USING SEARCH,12          Use 12 as Base Register 
         LA    14,SEARCHSV        Load SEARCHSV into 14
         ST    14,8(0,14)         Link backward pointer
         ST    13,4(0,14)         Link forward pointer
         LR    13,14              Load SEARCHSV
         LM    2,6,0(1)           Unload parameters
         SPACE
LOOP     XREAD 0(0,4),80          Read 80 bytes into BUFFER
         BC    B'0100',ENDLP      End of file?
         XDECI 7,0(0,4)           Empty line?
         BC    B'0001',LOOP       Back to loop
         LR    8,2                'Zero' the table pointer
LOOP2    C     8,0(0,3)             At EoT?
         BE    NOTFOUND           Not found
         C     7,0(0,8)           Found?
         BE    FOUND              Found           
         A     8,=F'4'            Increment table pointer
         B     LOOP2              Back to inner loop
         SPACE
FOUND    XDECO 7,FNUM             Send found num to line
         XPRNT FOUNDOUT,31        Print the line
         B     LOOP               Check for next number
         SPACE
NOTFOUND XDECO 7,NFNUM            Send not found num to line
         XPRNT NFOUNDO,35         Print the line
         B     LOOP               Check for next number
         SPACE
ENDLP    L     13,4(0,13)         Load register list
         LM    14,12,12(13)       Reload registers
         BR    14                 Branch back to MAIN
         LTORG
SEARCHSV DS    18F                Creates Save area for SEARCH
FOUNDOUT DC    C'-'               Found print line - 31 
         DC    C'TARGET ='
FNUM     DS    12C
         DC    C' was found'
NFOUNDO  DC    C'-'               Not found print line - 35
         DC    C'TARGET ='
NFNUM    DS    12C
         DC    C' was not found'    
         END   MAIN
/*
//FT05F001   DD  *
1 -2005         515   90  50      -1   172219           60205                                    

                       
-777224                                                                                          

                       
3       7534    204914           141  0 606031    -419                                           

                       
218 111 46                                                                                       

                       
9001  312909    -517  9 62  89  305   250 305                                                    

                       
7845902       -311      -100001                                                                  

                       
-999     2    6509    -5   177   42        777                                                   

                   
                                                                                                 

                       
345   -9009     65712                                                                            

                       

88442                                                                                            

                       
-84                 131  5106  121  -89 15   95221 -80                                           

                       
-5005005                                                                                         

                       
-77777777                                                                                        

                       
-8033
994 
9001
177               
2
321
-419
/*
//FT06F001   DD SYSOUT=*
//
