//KC03I36A JOB ,'Matt Rycraft',MSGCLASS=H
//STEP1 EXEC PGM=ASSIST
//STEPLIB DD DSN=KC02293.ASSIST.LOADLIB,DISP=SHR
//SYSPRINT DD SYSOUT=*
//SYSIN DD *
********************************************
* Assignment 7 - Pair Programmed
* Names: Tommy Franczak, Matt Rycraft
*
* Function: 
* 
* MAIN
* Register Usage
*
*  1     Parameter List
* 12     Base register
* 13     Main save
* 14     Return Address
********************************************
MAIN     CSECT
         STM   14,12,12(13)       Copy registers
         LR    12,15              Load 12 as base register
         USING MAIN,12            Use 12 as base register
         LA    14,MAINSAVE        Load MAINSAVE into R14
         ST    13,4(,14)          Link forward pointer
         ST    14,8(,13)          Link backward pointer
         LR    13,14              Load MAINSAVE
         SPACE
*
         LA    1,PLIST            Load PLIST into 1
         L     15,=V(BUILD)       Load virtual address of BUILD
         BALR  14,15              Do BUILD subroutine
         XDUMP TABLE,162
         SPACE
*
         LA    1,PLIST            Load PLIST into 1
         L     15,=V(PRINT)       Load virtual address of PRINT
         BALR  14,15              Do PRINT subroutine
         SPACE
*
         LA    1,PLIST            Load PLIST into 1
         L     15,=V(SORT)        Load virtual address of Sort
         BALR  14,15              Do SORT subroutine
         SPACE
*
         LA    1,PLIST            Load PLIST into 1
         L     15,=V(PRINT)       Load virtual address of PRINT
         BALR  14,15              Do PRINT subroutine
         SPACE
*         
         L     13,4(0,13)         Load backward pointer
         LM    14,12,12(13)       Load registers back
         BR    14
*         
         LTORG                    Literal origin
         ORG MAIN+((*-MAIN+31)/32)*32
TABLE    DS    120CL27            Declare entry table
EOT      DS    F                  Declare end of table 
BUFFER   DS    80C                Declare buffer
         DC    C'*'
*         
PAGENUM  DC    PL1'1'             packed number for page num
PRINTNUM DC    PL1'1'             Packed for print
PLIST    DC    A(TABLE)           Parameter list
         DC    A(EOT)
         DC    A(BUFFER)
         DC    A(PAGENUM)
         DC    A(PRINTNUM)
*         
MAINSAVE DS    18F                Declare MainSave
ENTRY    DSECT                    Dummy section for table - 27
STUDID   DS    4B                 Student ID
PASSWD   DS    12B                Password
PASSDT   DS    2B                 Password reset date
MAJOR    DS    4B                 Major
GPA      DS    2P                 Grade point average
CREDHR   DS    2B                 Credit Hours
ENROLIN  DS    1B                 Hours enrolled in
********************************************
* Build Subroutine
*
* Function: Reads in numbers from an 
* unknown file and builds a table of  
* 120 Full words, The element name, symbol
* the date the element was found and 
* the elements atomic weight 
* 
* Register Usage
*
*  4     Table Pointer
*  5     EOT Pointer
*  6     For CVD
*  9     Address of buffer
* 12     Base register
* 13     Address of BUILDSV (save area)
* 14     Return Address
********************************************
BUILD    CSECT
         STM   14,12,12(13)       Save registers
         LR    12,15              Load 12 as base register
         USING BUILD,12           Use 12 as base register
         LA    14,BUILDSV         Load buildsv into register 14
         ST    14,8(0,14)         Load backward pointer
         ST    13,4(0,14)         Load forward pointer
         LR    13,14              Load BuildSv
*         
         USING ENTRY,4            Starts using DSECT         
         LM    4,5,0(1)           Unload parameters 
         L     9,8(1)             Load address of buffer 
*         
BLOOP1   XREAD 0(0,9),80          Read into buffer 
         BC    B'0100',ENDBLP     END Loop
         PACK  TSTUDID(8),1(8,9)  Grab Student ID
         MVC   PASSWD(12),10(9)   Grab pass from line
         PACK  YEARP(8),27(4,9)   Grab Year from line
         PACK  DAYP(8),23(2,9)    Grab day from line
         PACK  MONTHP(8),25(2,9)  Grab Month from line
         MVC   MAJOR(4),32(9)     Grab Major, put in Table
         PACK  GPA(2),37(3,9)     Grab GPA from line
         PACK  TOTHOUR(8),41(3,9) Grab Total Hours from line
         PACK  HRENROLL(8),45(2,9) Grab Hours enrolled from line
*
*        Handle data manipulation to store in table
*
         CVB   6,TSTUDID          Convert TSTUDID to binary
         STCM  6,B'1111',STUDID   Put student id into table
*
         CVB   6,TOTHOUR          Convert ToTHOUR to binary
         STCM  6,B'0011',CREDHR   Put credit hours into table
*
         CVB   6,HRENROLL         Convernt HRENROLL to binary
         STCM  6,B'0001',ENROLIN  Put hours enrolled into table
*
         XC    PASSWD(4),STUDID   XOR Password encryption
         XC    PASSWD+4(4),STUDID
         XC    PASSWD+8(4),STUDID 
*
         SP    YEARP(8),=P'1980'  Subtract 1980 from year
         CVB   6,YEARP            Convert YEARP to binary
         SLL   6,9                Shift year into correct bits
         STCM  6,B'0011',PASSDT   Put year in table
*
         CVB   6,MONTHP           Shift month to binary
         SLL   6,5                Shift month into correct bit
         STCM  6,B'0011',TEMPB    Store into TEMPB
         OC    PASSDT(2),TEMPB    Add to DATE
*
         CVB   6,DAYP             Convert Day to binary
         STCM  6,B'0011',TEMPB    Store into TEMPB
         OC    PASSDT(2),TEMPB    Add to DATE
*
         LA    4,27(0,4)          Increment TABLE
         B     BLOOP1             Back to BLOOP1
*        
ENDBLP   ST    4,0(5)             Store EOT at last unused spot
         L     13,4(0,13)         Load register list
         LM    14,12,12(13)       Reload registers
         BR    14                 Branch back to MAIN
*         
         LTORG                                                          
BUILDSV  DS   18F                Allocate 18F for save area
EPHRASE  DS   12B                
PHRASE   DS   12B
         DS   0D                 Start on double boundary
TSTUDID  DS   8P                 Temp spot for Student ID
TOTHOUR  DS   8P                 Temp for total hours
HRENROLL DS   8P                 Temp for enrolled hours
YEARP    DS   8P                 Temp for year
MONTHP   DS   8P                 Temp for month
DAYP     DS   8P                 Temp for day
TEMPB    DS   2B                 Temp for converting to binary
*
********************************************
* Print Subroutine
*
* Function: Prints the contents of the table
* no more then 50 per page. 
* 
* Register Usage
*
*  4     Table Pointer
*  5     EOT Pointer
*  6     First nonspace of digit
* 10     Address of pagenum
* 11     Address of printnum
* 12     Base register
* 13     Address of BUILDSV (save area)
* 14     Return Address
******************************************** 
PRINT    CSECT
         STM   14,12,12(13)       Save registers
         LR    12,15              Load 12 as base register
         USING PRINT,12           Use 12 as base register
         LA    14,PSAVE           Load buildsv into register 14
         ST    14,8(0,14)         Load backward pointer
         ST    13,4(0,14)         Load forward pointer
         LR    13,14              Load BuildSv
*         
         USING ENTRY,4            
         LM    4,5,0(1)           Unload parameters 
         L     9,8(1)             Load address of buffer
         L     10,12(1)           Load address of pagenum 
         L     11,16(1)           Load address of printnum
*
         CP    0(1,11),=P'1'      First print?
         BE    PRNTF              Go to prntf
         MVC   PAGEHERE(2),=X'4020' Prepare for pagenum
         ED    PAGEHERE(2),0(10)  Put pagenum on line
         XPRNT SHEAD,78           Print sorted header
         B     MEET               Meet back up
PRNTF    MVC   PAGEHER2(2),=X'4020' Prepare for pagenum
         ED    PAGEHER2(2),0(10)  Put pagenum on line
         XDUMP PAGEHER2,32
         XPRNT FHEAD,78
MEET     AP    0(1,10),=P'1'      Increment pagenum
         AP    0(1,11),=P'1'      Increment printnum
         XPRNT DTEXT1,78          Print descriptive headers
         XPRNT DTEXT2,78      
*
PLOOP    C     4,0(0,5)           At EoT?
         BE    ENDPT              End print
*
*        Move print patterns for ED
*
         MVC   STUDNUM(11),=X'4021C72020202020202020'
         MVC   RENEWDT(12),=X'402120206120206120202020'
         MVC   GPAO(7),=X'402021204B2020'
         MVC   TOTHRO(4),=X'40202120'
         MVC   CURHRO(4),=X'40202120'
*
*        Put Student ID on line
*
         SR    6,6                Zero register 6
         ICM   6,B'1111',STUDID   Move STUDID into R6
         CVD   6,TEMPDEC          Convert STUDID to dec
         ED    STUDNUM(11),TEMPDEC+3 Send Student Id to line
* 
*        Put password on line
*
         MVC   TEMPCHAR(12),PASSWD Put PASSWD into temp
         XC    TEMPCHAR(4),STUDID     Decrypt password
         XC    TEMPCHAR+4(4),STUDID
         XC    TEMPCHAR+8(4),STUDID
         MVC   PASSWORD(12),TEMPCHAR Put passwd on line
* 
*        Put renewal Date on line        
*
         SR    6,6                Zero R6
         ICM   6,B'1100',PASSDT   Put date into r6
         SRL   6,25               Make year alone
         CVD   6,TEMPDEC          Put year into temp
         AP    TEMPDEC(8),=P'1980' Add 1980 back
*
         SR    6,6                Zero R6
         ICM   6,B'1100',PASSDT   Put date into r6
         SLL   6,7                Strip off year
         SRL   6,28               Strip day
         CVD   6,TEMPDEC2         Put month into temptemp
         SRP   TEMPDEC2(8),4,0    Shift left 4
         AP    TEMPDEC(8),TEMPDEC2(8) Append to year
*
         SR    6,6                Zero R6
         ICM   6,B'1100',PASSDT   Put date into R6
         SLL   6,11               Strip year and month
         SRL   6,27               Position day correctly
         CVD   6,TEMPDEC2         Put day into temptemp
         SRP   TEMPDEC2(8),6,0    Shift left 6
         AP    TEMPDEC(8),TEMPDEC2(8) Apppend to month and year
*
         ED    RENEWDT(12),TEMPDEC+3 Put date on line
*
*        Put major on line
*
         MVC   MAJORO(4),MAJOR    Move major from table to line
*
*        Put GPA on line
*
         ZAP   TEMPDEC(8),GPA(2)  Put GPA into temp
         ED    GPAO(7),TEMPDEC+5  Fill pattern at GPAO on line
*
*        Put total hours on line.
*
         SR    6,6                Zero register 6
         IC    6,CREDHR           Put first byte of credhr in
         SLL   6,1                Shift to correct position
         IC    6,CREDHR+1         Put second byte of credhr in
         CVD   6,TEMPDEC          Convert to decimal, put in temp
         ED    TOTHRO(4),TEMPDEC+6 Put temp on the line
*
*        Put current hours on line.
*
         SR    6,6                Zero register 6
         IC    6,ENROLIN          Put enrolin in 6
         CVD   6,TEMPDEC          Convert to decimal, put in temp
         ED    CURHRO(4),TEMPDEC+6 Put temp on the line
*
         XPRNT OUTLINE,77
         LA    4,27(0,4)          Increment table
         B     PLOOP
*
ENDPT    XPRNT SPACES,80          Empty line 
         L     13,4(0,13)         Load register list
         LM    14,12,12(13)       Reload registers
         BR    14                 Branch back to MAIN
*
         LTORG
PSAVE    DS    18F                Print Save area
*
OUTLINE  DC    C' '               Prints student info
STUDNUM  DS    11C                12
         DC    3C' '              15
PASSWORD DS    12C                27
         DC    4C' '              31
RENEWDT  DS    12C                43
         DC    5C' '              48
MAJORO   DS    4C                 52
         DC    2C' '              54
GPAO     DS    7C                 61
         DC    4C' '              65
TOTHRO   DS    4C                 69
         DC    4C' '              73
CURHRO   DS    4C                 77
*
SHEAD    DC    C'1'               Print sorted header
         DC    26C' '             27
         DC    C'Student Information by GPA' 53
         DC    C'                 Page  '    76
PAGEHERE DS    2C                 78
*
FHEAD    DC    C'1'               Print sorted header
         DC    26C' '             27
         DC    C'Student Information       ' 53
         DC    C'                 Page  '    76
PAGEHER2 DC    2C' '              78
*
DTEXT1   DC    C'-'               Descriptive header 1-78
         DC    C'  Student                               '
         DC    C'                       Total  Current'
*
DTEXT2   DC    C' '               Descriptive header 2-78
         DC    C'   ID        Password         Renewal Da'
         DC    C'te   Major     GPA     Hours    Hours'
*
SPACES   DC    80C' '             Empty line for output formatting
*
         DS    D                  Puts on doubleword
TEMPDEC  DS    8P                 Temp for decimals
TEMPDEC2 DS    8P                 Temp for temp (tempception)
TEMPCHAR DS    12C                Temp for password
********************************************
* Sort Subroutine
*
* Function: Sorts the table alphabetically
* calls print to reprint the newly sorted 
* table
* 
* Register Usage
*
*  4     Second loop table pointer
*  5     EOT Pointer
*  6     First loop table pointer
* 12     Base register
* 13     Address of BUILDSV (save area)
* 14     Return Address
********************************************
SORT     CSECT
         STM   14,12,12(13)         Save Registers 
         LR    12,15                Load Registers
         USING SORT,12              Use 12 as base register
         LA    14,SORTSV            Load SORTSV into R14
         ST    14,4(14)             Load backwards pointer
         ST    13,4(14)             Load forwards pointer
         LR    13,14                Load SORTSV
*
         USING ENTRY,4            
         LM    4,5,0(1)             Unload parameters 
*
*        External loop
*
         LR    6,4
SLOOP1   LR    4,6                  Reload 4's value
         C     6,0(0,5)             At eot?
         BE    ENDSLP1              Go to end
*
*        Internal loop
*
SLOOP2   C     4,0(0,5)             At eot?
         BE    ENDSLP2              Go to first loop
*
         CP    GPA(2),22(2,6)       Current GPA > Other?
         BH    SWAP                 Swap elements
*
AFTRSWAP LA    4,27(0,4)            Increment table ptr
         B     SLOOP2               Back to second loop
*
SWAP     MVC   TEMPLINE(27),0(4)    Copy Current into temp
         MVC   0(27,4),0(6)         Copy other into current
         MVC   0(27,6),TEMPLINE     Copy temp into other
         B     AFTRSWAP
*
*        End Internal
*
ENDSLP2  LA    6,27(0,6)            Increment table ptr
         B     SLOOP1               Back to first loop
*
*        End external
*
ENDSLP1  L     13,4(0,13)           Load register list
         LM    14,12,12(13)         Reload registers
         BR    14       
*
         LTORG
SORTSV   DS    18F                  Sort CSECT save area
TEMPLINE DS    27B                  Temporary table line
         END
//FT05F001  DD  DSN=KC02314.AUTUMN17.CSCI360.HW8DATA,DISP=SHR
