//KC03I36A JOB ,'Matt Rycraft',MSGCLASS=H
//STEP1 EXEC PGM=ASSIST
//STEPLIB DD DSN=KC02293.ASSIST.LOADLIB,DISP=SHR
//SYSPRINT DD SYSOUT=*
//SYSIN DD *
********************************************
* Assignment 7 - Pair Programmed
* Names: Tommy Franczak, Matt Rycraft
*
* Function: 
* 
* MAIN
* Register Usage
*
*  1     Parameter List
* 12     Base register
* 13     Main save
* 14     Return Address
********************************************
MAIN     CSECT
         STM   14,12,12(13)       Copy registers
         LR    12,15              Load 12 as base register
         USING MAIN,12            Use 12 as base register
         LA    14,MAINSAVE        Load MAINSAVE into R14
         ST    13,4(,14)          Link forward pointer
         ST    14,8(,13)          Link backward pointer
         LR    13,14              Load MAINSAVE
         SPACE
*
         LA    1,PLIST            Load PLIST into 1
         L     15,=V(BUILD)       Load virtual address of BUILD
         BALR  14,15              Do BUILD subroutine
         SPACE
*         
         LA    1,PLIST            Load PLIST into 1
         L     15,=V(PRINT)       Load virtual address of PRINT
         BALR  14,15              Do PRINT subroutine
         SPACE
*
         LA    1,PLIST            Load PLIST into 1
         L     15,=V(SORT)        Load virtual address of SORT
         BALR  14,15              Do SORT subroutine
         SPACE
*
         LA    1,PLIST            Load PLIST into 1
         L     15,=V(PRINT)       Load virtual address of PRINT
         BALR  14,15              Do PRINT subroutine
         SPACE
*
         L     13,4(0,13)         Load backward pointer
         LM    14,12,12(13)       Load registers back
         BR    14
*         
         LTORG                    Literal origin
         ORG MAIN+((*-MAIN+31)/32)*32
TABLE    DS    120CL25            Declare entry table
EOT      DS    F                  Declare end of table 
BUFFER   DS    80C                Declare buffer
         DC    C'*'
*         
PAGENUM  DC    PL1'1'             ! packed number for page num
PLIST    DC    A(TABLE)           Parameter list
         DC    A(EOT)
         DC    A(BUFFER)
         DC    A(PAGENUM)
*         
MAINSAVE DS    18F                Declare MainSave
ENTRY    DSECT                    Dummy section for table
DATE     DS    CL7                Date = 7 characters long 
NAME     DS    CL13               Name = 13 characters long
SYMBOL   DS    CL3                Symbol = 3 characters long
NUMBER   DS    PL2                Number = 2 packed num
********************************************
* Build Subroutine
*
* Function: Reads in numbers from an 
* unknown file and builds a table of  
* 120 Full words, The element name, symbol
* the date the element was found and 
* the elements atomic weight 
* 
* Register Usage
*
*  4     Table Pointer
*  5     EOT Pointer
*  6     First nonspace of digit
*  8     Address of Outline variables
*  9     Address of buffer
* 12     Base register
* 13     Address of BUILDSV (save area)
* 14     Return Address
********************************************
BUILD    CSECT
         STM   14,12,12(13)       Save registers
         LR    12,15              Load 12 as base register
         USING BUILD,12           Use 12 as base register
         LA    14,BUILDSV         Load buildsv into register 14
         ST    14,8(0,14)         Load backward pointer
         ST    13,4(0,14)         Load forward pointer
         LR    13,14              Load BuildSv
*         
         USING ENTRY,4            
         LM    4,5,0(1)           Unload parameters 
         L     9,8(1)             Load address of buffer 
*         
BLOOP1   XREAD 0(0,9),80          Read into buffer 
         BC    B'0100',ENDBLP     END Loop
         L     8,=F'4'            Amount of args
         LR    1,9
BLOOP2   TRT   0(80,1),NOTSPACE   find first non-empty
         LR    6,1                Points R6 to first non-empty location
         TRT   0(80,1),SPACES     finds first empty space
         LR    7,1                Points R7 to first empty location
         SR    7,6                Set R7 to length of data
         BCTR  7,0                Decrement R7 to real length
*
         C     8,=F'4'            First col?
         BE    ELEDATE            
         C     8,=F'3'            Second col?
         BE    ELENAME
         C     8,=F'2'            Third col?
         BE    ELESYM
         C     8,=F'1'            Fourth col?
         BE    ELENUM
*
ELEDATE  MVC   DATE,=CL7' '       Initialize date 
         EX    7,TDATE            Put date into Table
         TR    DATE(1),TRTOUP     Capitalize first
         TR    DATE+1(6),TRTODOWN Lowercase rest of letters
         BCT   8,BLOOP2      
*        
ELENAME  MVC   NAME(13),=CL13' '  Initialize name
         EX    7,TNAME            Put name into table
         TR    NAME(1),TRTOUP     Capitalize first
         TR    NAME+1(12),TRTODOWN Lowercase rest of letters
         LA    10,TNUM            load address of 1st EX statement
         BCT   8,BLOOP2
*
ELESYM   MVC   SYMBOL(3),=CL2' '  Initialize symbol
         EX    7,TSYMBOL          Put symbol into the table
         TR    SYMBOL(1),TRTOUP     Capitalize first
         TR    SYMBOL+1(12),TRTODOWN Lowercase rest of letters
         BCT   8,ELENUM
*
ELENUM   TRT   0(80,1),DIGITS     Find next digit
         LR    6,1                Sets R6 to location of digit
         TRT   0(80,1),SPACES     Find next empty space
         LR    7,1                Sets R7 to location of empty space
         SR    7,6                Sets R7 to length of number
         BCTR  7,0                Decrement R7 to actual length
         EX    7,TNUM             Put number into Table
         LA    4,25(0,4)          Increment table pointer
         B     BLOOP1             Return to BLOOP1
*         
ENDBLP   ST    4,0(5)             Store EOT at last unused spot
         L     13,4(0,13)         Load register list
         LM    14,12,12(13)       Reload registers
         BR    14                 Branch back to MAIN
*         
         LTORG                                                          
BUILDSV  DS    18F                Allocate 18F for save area         
NOTSPACE DC    256X'FF'           Create search table to            
         ORG   NOTSPACE+C' '      search for first non-empty        
         DC    X'00'              space                             
         ORG                                                          
SPACES   DC    256X'00'           Create search table to            
         ORG   SPACES+C' '        search for first empty            
         DC    X'FF'              space                             
         ORG  
DIGITS   DC    256X'00'
         ORG   DIGITS+X'F0'
         DC    10X'01'
         ORG
TDATE    MVC   DATE(0),0(6)       EX for Date                
TNAME    MVC   NAME(0),0(6)       EX for Name                  
TSYMBOL  MVC   SYMBOL(0),0(6)   EX for Symbol                 
TNUM     PACK  NUMBER(2),0(0,6)   EX for Number
*
TRTOUP   DC    X'000102030405060708090A0B0C0D0E0F' To uppercase
         DC    X'101112131415161718191A1B1C1D1E1F'
         DC    X'202122232425262728292A2B2C2D2E2F'          
         DC    X'303132333435363738393A3B3C3D3E3F'
         DC    X'404142434445464748494A4B4C4D4E4F'
         DC    X'505152535455565758595A5B5C5D5E5F'
         DC    X'606162636465666768696A6B6C6D6E6F'
         DC    X'707172737475767778797A7B7C7D7E7F'
         DC    X'80C1C2C3C4C5C6C7C8C98A8B8C8D8E8F'
         DC    X'90D1D2D3D4D5D6D7D8D99A9B9C9D9E9F'
         DC    X'A0A1E2E3E4E5E6E7E8E9AAABACADAEAF'
         DC    X'B0B1B2B3B4B5B6B7B8B9BABBBCBDBEBF'
         DC    X'C0C1C2C3C4C5C6C7C8C9CACBCCCDCECF'
         DC    X'D0D1D2D3D4D5D6D7D8D9DADBDCDDDEDF'
         DC    X'E0E1E2E3E4E5E6E7E8E9EAEBECEDEEEF'
         DC    X'F0F1F2F3F4F5F6F7F8F9FAFBFCFDFEFF'
*
TRTODOWN DC    X'000102030405060708090A0B0C0D0E0F' To lowercase
         DC    X'101112131415161718191A1B1C1D1E1F'
         DC    X'202122232425262728292A2B2C2D2E2F'          
         DC    X'303132333435363738393A3B3C3D3E3F'
         DC    X'404142434445464748494A4B4C4D4E4F'
         DC    X'505152535455565758595A5B5C5D5E5F'
         DC    X'606162636465666768696A6B6C6D6E6F'
         DC    X'707172737475767778797A7B7C7D7E7F'
         DC    X'808182838485868788898A8B8C8D8E8F'
         DC    X'909192939495969798999A9B9C9D9E9F'
         DC    X'A0A1A2A3A4A5A6A7A8A9AAABACADAEAF'
         DC    X'B0B1B2B3B4B5B6B7B8B9BABBBCBDBEBF'
         DC    X'C0818283848586878889CACBCCCDCECF'
         DC    X'D0919293949596979899DADBDCDDDEDF'
         DC    X'E0E1A2A3A4A5A6A7A8A9EAEBECEDEEEF'
         DC    X'F0F1F2F3F4F5F6F7F8F9FAFBFCFDFEFF'
********************************************
* Print Subroutine
*
* Function: Prints the contents of the table
* no more then 50 per page. 
* 
* Register Usage
*
*  4     Table Pointer
*  5     EOT Pointer
*  6     First nonspace of digit
*  8     Address of Outline variables
*  9     Loop Counter
* 12     Base register
* 13     Address of BUILDSV (save area)
* 14     Return Address
******************************************** 
PRINT    CSECT
         STM   14,12,12(13)       Save registers
         LR    12,15              Load 12 as base register
         USING PRINT,12           Use 12 as base register
         LA    14,PSAVE           Load buildsv into register 14
         ST    14,8(0,14)         Load backward pointer
         ST    13,4(0,14)         Load forward pointer
         LR    13,14              Load BuildSv
*         
         USING ENTRY,4            
         LM    4,5,0(1)           Unload parameters 
         L     9,8(1)             Load address of buffer 
*        
*PLOOP1   XPRNT 0(7),80            Print CAPTION line
PLOOP1   XPRNT HEADER,69          Print HEADER line
         XPRNT EMPTYLN,1          Print empty line after header
         SR    9,9                Zero loop counter     
*
PLOOP2   MVI   LINE+1,C' '        Clear LINE
         MVC   LINE+1(78),LINE+1  Clear LINE
         C     4,0(0,5)           EOT = Table Ptr?
         BE    ENDPLOOP           Go to ENDPLOOP
         MVC   ELNAME(13),NAME    PUT NAME ON LINE
         MVC   ELSYMBOL(3),SYMBOL Put Symbol on line
         MVC   ATNUM(4),=X'40202120'   
         ED    ATNUM(4),NUMBER    Put Number on line
         MVC   ELDATE(7),DATE     Put Date on line
         XPRNT LINE,62            Print line 
         A     4,=F'25'           Increment table pointer
         A     9,=F'1'            Increment counter
         C     9,=F'50'           Is counter at 50?
         BNE   PLOOP2             Start loop over
         XPRNT EMPTYLN,1          Print empty line
         XPRNT EMPTYLN,1          Print empty line
         B     PLOOP1             Start PLOOP1 again
*        
ENDPLOOP DS    0H                 ENDPLOOP lable
         L     13,4(0,13)         Load register list
         LM    14,12,12(13)       Reload registers
         BR    14                 Branch back to MAIN
*
         LTORG
PSAVE    DS    18F                Print Save area
HEADER   DC    C'-'               Format for header line - 69
         DC    C'Name of Element'
         DC    3C' '
         DC    C'Chemical Symbol'
         DC    3C' '
         DC    C'Atomic Number'
         DC    4C' '
         DC    C'Date Discovered'
LINE     DC    C'0'               Declare print line - 37 
EMPTYLN  DC    C' '               Blank line for printing
ELNAME   DS    13C
         DC    4C' '
ELSYMBOL DS    3C
         DC    14C' '
ATNUM    DS    2C
         DC    17C' '
ELDATE   DS    7C
********************************************
* Sort Subroutine
*
* Function: Sorts the table alphabetically
* calls print to reprint the newly sorted 
* table
* 
* Register Usage
*
*  4     Table Pointer
*  5     EOT Pointer
*  6     Holds beginning of table
*  7     Inner ptr
*  8     Inner ptr-1
*  9     Temp for ptr subtraction
* 12     Base register
* 13     Address of BUILDSV (save area)
* 14     Return Address
********************************************
SORT     CSECT
         STM   14,12,12(13)         Save Registers 
         LR    12,15                Load Registers
         USING SORT,12              Use 12 as base register
         LA    14,SORTSV            Load SORTSV into R14
         ST    14,4(14)             Load backwards pointer
         ST    13,4(14)             Load forwards pointer
         LR    13,14                Load SORTSV
*
         USING ENTRY,4            
         LM    4,5,0(1)             Unload parameters 
*
         XPRNT SHEAD,80             Print Sort header
         LR    6,4                  6 holds begin table
SORT1    A     4,=F'25'             Increment pointer
         C     4,0(0,5)             At EOT?
         BE    ENDSORT              If = end sort
         LR    7,4                  7 holds temporary 4
*
SORT2    CR    4,6                  At begin table?
         BE    END2
         MVC   VALUE(25),DATE       Set value to current name              
         S     4,=F'25'
         MVC   VALUEL(25),DATE      Set valuel to previous name
         CLC   VALUE+7(13),VALUEL+7 Is inner ptr < inner-1?
         BC    B'0100',SWAP         Swap values if R4 is less
         B     END2                Else, go to first loop
*
SWAP     MVC   DATE(25),VALUE       Swap later value with earlier
         A     4,=F'25'
         MVC   DATE(25),VALUEL      Swap earlier with later
         S     4,=F'25'
         B     SORT2     
*
END2     LR    4,7                  Restore 4
         B     SORT1                Go back to outerloop
*      
ENDSORT  L     13,4(0,13)           Load register list
         LM    14,12,12(13)         Reload registers
         BR    14                   Branch back to MAIN
*         
         LTORG
SHEAD    DC    C'1'
         DC    19C' '
         DC    C'Chronological List of Elements'
VALUE    DS    25C                  Current table value
VALUEL   DS    25C                  Previous table value
TEMP     DS    13C                  Temp value for swap 
SORTSV   DS    18F
TNAME7   MVC   VALUE(0),0(7)        EX for Name
TNAME8   MVC   VALUEL(0),0(8)
         END
//FT05F001  DD  DSN=KC02314.AUTUMN17.CSCI360.HW7DATA,DISP=SHR
