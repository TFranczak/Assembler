//KC03I36A JOB ,'Tommy Franczak',MSGCLASS=H
//STEP1 EXEC PGM=ASSIST
//STEPLIB DD DSN=KC02293.ASSIST.LOADLIB,DISP=SHR
//SYSPRINT DD SYSOUT=*
//SYSIN DD *
********************************************
* Assignment 3 - Pair Programmed
* Names: Tommy Franczak, Matt Rycraft
* 
* Register Usage
*
*  2,3   Num 1 - Even Odd Pair
*  4     Num 2
*  8     Total Count
*  9     Operation Type
* 10     Addition Count
* 11     Subtraction Count
* 12     Multiplication Count
* 13     Division Count    
* 15     Base Register
********************************************
MAIN     CSECT
         USING MAIN,15
         TITLE 'Main Loop'    Sets page title
         SR    10,10            Zero Operation Counts
         SR    11,11
         SR    12,12
         SR    13,13
         SR    8,8
         XPRNT PRGHDR,70        Prints program header
         XPRNT COLHDR,53        Prints column proceedings
         XPRNT LINEBRK,109      Prints line break 
LOOP     XREAD BUFFER,80        Read from stdin into buffer
         BC    B'0100',ENDLOOP  End loop if EOF reached
         SR    2,2              Assure Even Odd Pair is zeroed
         XDECI 9,BUFFER         Read operation type
         XDECI 3,0(1)           Read first num
         XDECI 4,0(1)           Read second num
         C     9,=F'1'          Is addition?
         BE    ADD
         C     9,=F'2'          Is subtraction?
         BE    SUB
         C     9,=F'3'          Is multiplication?
         BE    MULT
         C     9,=F'4'          Is division?
         BE    DIV  
AFTEROP  A     8,=F'1'         Increment total count
         B     LOOP             Back to top
         SPACE
         TITLE 'Operation calculations and print statements'
         EJECT
ADD      XDECO 3,ANUM1          Format first num for output
         XDECO 4,ANUM2          Format second num for output
         AR    3,4              Add both nums
         XDECO 3,ARES           Format result for output
         XPRNT ADDLINE,71       Print ADDLINE
         A     10,=F'1'         Increment Addition Count
         B     AFTEROP          Goes back to loop
         SPACE
SUB      XDECO 3,SNUM1          Format first num for output
         XDECO 4,SNUM2          Format second num for output
         SR    3,4              Subtract both nums
         XDECO 3,SRES           Format result for output
         XPRNT SUBLINE,71       Print SUBLINE
         A     11,=F'1'         Increment Subtraction Count
         B     AFTEROP          Goes back to loop
         SPACE
MULT     XDECO 3,MNUM1          Format first num for output
         XDECO 4,MNUM2          Format second num for output
         SR    2,2
         MR    2,4              Multiply both nums
         XDECO 3,MRES           Format result for output
         XPRNT MULTLINE,71      Prints MULTLINE output
         A     12,=F'1'         Increment Multiplication Count
         B     AFTEROP          Goes back to loop
         SPACE
DIV      XDECO 3,DNUM1          Format first num for output
         XDECO 4,DNUM2          Format second num for output
         M     2,=F'1'          Make R3 64-bit number
         DR    2,4              Divide first num by second
         XDECO 3,DRES           Format result for output
         XDECO 2,DREM           Format remainder for output
         XPRNT DIVLINE,101      Prints DIVLINE output
         A     13,=F'1'         Increment Division Count
         B     AFTEROP          Goes back to loop
         SPACE
ENDLOOP  XDECO 8,TTLCNT         Sends total count to output
         XPRNT TTLOUT,32        Prints total count for output
         XDECO 10,ADCOUNT       Sends add count to output     
         XDECO 11,SBCOUNT       Sends sub count to output
         XDECO 12,MTCOUNT       Sends mult count to output
         XDECO 13,DVCOUNT       Sends div count to output
         XPRNT OPCNTS,103       Prints all op counts
         BR    14               Branches to end of program
         SPACE
         TITLE 'Variables Declarations and program end'
         EJECT
PRGHDR   DC    C' '             Formats program header - 70 long
         DC    48C' '           
         DC    C'Results of Arithmetic'
         SPACE
COLHDR   DC    C'0'             Formats column header - 53 long
         DC    C'Operation             '
         DC    C'Num 1       '
         DC    C'Num 2       '
         DC    C'Result'
         SPACE
LINEBRK  DC    C' '             Creates line break - 109 long
         DC    108C'-'
         SPACE
TTLOUT   DC    C'-'             Formats Total count line - 32 long
         DC    C'Total Calculations:'
TTLCNT   DS    12C
         SPACE
OPCNTS   DC    C' '             Formats op count lines - 103 long
         DC    C'Addition:'
ADCOUNT  DS    12C
         DC    3C' '
         DC    C'Subtraction:'
SBCOUNT  DS    12C
         DC    3C' '
         DC    C'Multiplication:'
MTCOUNT  DS    12C
         DC    3C' '
         DC    C'Division:'
DVCOUNT  DS    12C
         SPACE
BUFFER   DS    80C              Creates BUFFER variable
         SPACE
ADDLINE  DC    C'0'             Formats the ADDLINE out - 71 long
         DC    C'Addition       '
ANUM1    DS    12C
ANUM2    DS    12C
         DC    C'        Sum:       '
ARES     DS    12C
         SPACE
SUBLINE  DC    C'0'             Formats the SUBLINE out - 71 long
         DC    C'Subtraction    '
SNUM1    DS    12C
SNUM2    DS    12C
         DC    C'        Difference:'
SRES     DS    12C              
         SPACE
MULTLINE DC    C'0'             Formats the MULTLINE out - 71 long
         DC    C'Multiplication:'
MNUM1    DS    12C
MNUM2    DS    12C
         DC    C'        Product:   '
MRES     DS    12C
         SPACE
DIVLINE  DC    C'0'             Formats the DIVLINE out - 101 long
         DC    C'Division       '
DNUM1    DS    12C
DNUM2    DS    12C
         DC    C'        Quotient:  '
DRES     DS    12C
         DC    C'        Remainder:'   
DREM     DS    12C
         LTORG                  Sets the literal Origin
         SPACE
         END   MAIN
/*
//FT05F001 DD DSN=KC02314.AUTUMN17.CSCI360.HW3DATA,DISP=SHR
//